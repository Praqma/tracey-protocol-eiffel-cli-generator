apply plugin: 'application'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'net.sourceforge.argparse4j:argparse4j:0.7.0'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.21'
    compile('com.github.Praqma:tracey-protocol-eiffel:master') {
        changing = true
    }
}


configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

mainClassName = 'net.praqma.tracey.protocol.eiffel.cli.Main'

task runDebug(type: JavaExec, dependsOn: classes) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args(['EiffelSourceChangeCreatedEvent', '-f', 'out.json'])
}

task uberjar(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from {configurations.compile.collect {zipTree(it)}} {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
    	attributes 'Main-Class': mainClassName
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'net.praqma.tracey.protocol.eiffel.cli'
            artifactId 'tracey-protocol-eiffel-cli-generator'
            from components.java
        }
    }
}